* LIFEG1R 2023-05-14
* 64 X 32, ASCII SCREEN
	ORG	$4000
ZB	JMP	START
	FDB	ZE-ZB
START
* PAGE 0 @ $7A00
* PAGE 1 @ $7C00
* SPACE FOR STACK @ $7F00
* REQUIRED EQUATES
PG0	EQU	$7D00
PG1	EQU	$7E00
PGSZ	EQU	$0100
PGRWSZ	EQU	$08
* TELL CORE TO CPYBTS
CPYBT	EQU	1

* INCLUDE CORE ROUTINE
	INCLUDE	LIFECORE.asm
*
* COPY PG0 TO VIDRAM
* BYTE FORMAT
* BOTTOM NIBBLE:
* *******
* * 8 4 *
* * 2 1 *
* *******
* TOP NIBBLE IS COLOUR
* BUFF IS $40
* HIGH BIT SET FOR GFX
CPYBTS	LDX	#PG0
	LDY	#VIDRAM
CPYBT1	LDB	#8
CPYBT2	LDA	,X
	ANDA	#$C0
	LSRA
	LSRA
	LSRA
	LSRA
	STA	,Y
	LDA	8,X
	ANDA	#$C0
	LSRA
	LSRA
	LSRA
	LSRA
	LSRA
	LSRA
	ORA	,Y
	ORA	#$80+$40
	STA	,Y+
	LDA	,X
	ANDA	#$30
	LSRA
	LSRA
	STA	,Y
	LDA	8,X
	ANDA	#$30
	LSRA
	LSRA
	LSRA
	LSRA
	ORA	,Y
	ORA	#$80+$40
	STA	,Y+
	LDA	,X
	ANDA	#$0C
	STA	,Y
	LDA	8,X
	ANDA	#$0C
	LSRA
	LSRA
	ORA	,Y
	ORA	#$80+$40
	STA	,Y+
	LDA	,X
	ANDA	#$03
	LSLA
	LSLA
	STA	,Y
	LDA	8,X
	ANDA	#$03
	ORA	,Y
	ORA	#$80+$40
	STA	,Y+
	LEAX	1,X
	DECB
	BNE	CPYBT2
	LEAX	8,X
	CMPY	#VIDRAM+$200
	BLO	CPYBT1
	RTS

* INCLUDES
	INCLUDE	RND.asm
	INCLUDE	ROMS.asm
	INCLUDE	TBLGEN.asm
	* ASSERT, PLEASE?
	* CANNOT RESOLVE ON 1ST PASS
	*IFLE PG0-POPCNT-128
	*ERROR "HIT GRAPHICS MEM!"
	*ENDC
ZE	EQU	*
	END	ZB
