
10 ' LIFETBL/BAS:1 2021-07-10
20 ' GENERATE ASM LOOKUP TABLES
30 ' FOR FAST VERSION OF
40 ' CONWAY'S LIFE
50 ' INDEXED BY A *SIGNED* INT8
60 DIM T$(6)
70 FOR I=1TO6
80 READ I$:T$(I)=I$
90 NEXT I
100 FOR F=1 TO 6
110 F$="LIFETBL"+RIGHT$(STR$(F),1)+"/ASM"
120 OPEN"O",#1,F$
130 L=1
140 GOSUB610
150 L$=L$+"* GENERATED BY LIFETBL/BAS"
160 PRINT L$
170 PRINT #1,L$
180 FOR I=128TO255
190 ON F GOSUB 350,380,410,460,510,560
200 GOSUB670
210 PRINT L$
220 PRINT #1,L$
230 NEXT I
240 FOR I=0TO127
250 ON F GOSUB 350,380,410,460,510,560
260 GOSUB670
270 PRINT L$
280 PRINT #1,L$
290 NEXT I
300 CLOSE#1
310 NEXT F
320 END
330 DATA"LOCNT","HICNT","LTSUM","RTSUM","LTMSK","RTMSK"
340 ' LOCNT
350 B=(I AND 1)+(I AND 2)*2+(I AND 4)*4+(I AND 8)*8
360 RETURN
370 ' HICNT
380 B=(I AND 16)/16+(I AND 32)/8+(I AND 64)/4+(I AND 128)/2
390 RETURN
400 ' LTSUM
410 B=0
420 IF (I AND 12)/4+(I AND 48)/16+(I AND 192)/64 = 3 THEN B=4
430 IF (I AND 3)+(I AND 48)/16+(I AND 192)/64 = 3 THEN B=B+8
440 RETURN
450 ' RTSUM
460 B=0
470 IF (I AND 3)+(I AND 12)/4+(I AND 192)/64 = 3 THEN B=1
480 IF (I AND 3)+(I AND 12)/4+(I AND 48)/16 = 3 THEN B=B+2
490 RETURN
500 ' LTMSK
510 B=0
520 IF (I AND 12)/4+(I AND 48)/16+(I AND 192)/64 = 4 THEN B=4
530 IF (I AND 3)+(I AND 48)/16+(I AND 192)/64 = 4 THEN B=B+8
540 RETURN
550 ' RTMSK
560 B=0
570 IF (I AND 3)+(I AND 12)/4+(I AND 192)/64 = 4 THEN B=1
580 IF (I AND 3)+(I AND 12)/4+(I AND 48)/16 = 4 THEN B=B+2
590 RETURN
600 ' LEFT-ZERO PADDED LINE NO
610 L$=STR$(L)
620 L$=RIGHT$(L$,LEN(L$)-1)
630 L$=STRING$(5-LEN(L$),"0")+L$+" "
640 L=L+1
650 RETURN
660 ' DUMP ASSEMBLY
670 GOSUB610
680 IF I=0 THEN L$=L$+T$(F)
690 L$=L$+CHR$(9)+"FCB"+CHR$(9)+"$"+HEX$(B)
700 RETURN
